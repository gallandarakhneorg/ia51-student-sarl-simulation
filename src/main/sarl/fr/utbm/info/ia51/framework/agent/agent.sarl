/* 
 * $Id$
 * 
 * Copyright (c) 2014-17 Stephane GALLAND <stephane.galland@utbm.fr>.
 * 
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 * 
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 * This program is free software; you can redistribute it and/or modify
 */
package fr.utbm.info.ia51.framework.^agent

import fr.utbm.info.ia51.framework.environment.DynamicType
import fr.utbm.info.ia51.framework.environment.Influence
import fr.utbm.info.ia51.framework.environment.Percept
import fr.utbm.info.ia51.framework.environment.SimulationAgentReady
import fr.utbm.info.ia51.framework.environment.StopSimulation
import fr.utbm.info.ia51.framework.math.Vector2f
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import java.util.List
import java.util.UUID
import io.sarl.core.DefaultContextInteractions

agent AbstractAnimat {
	
	uses PhysicEnvironment, Lifecycle, DefaultContextInteractions
	
	var behaviorType : DynamicType
	
	on Initialize {
		occurrence.overridableInitializationStage
		emit(new SimulationAgentReady)
	}

	/** Change the type of the behavior.
	 *
	 * @param type the type.
	 */
	def setBehaviorType(type : DynamicType) {
		this.behaviorType = type
	}

	/** Replies the type of the behavior.
	 * 
	 * @param type the type.
	 */
	def getBehaviorType() : DynamicType {
		this.behaviorType
	}

	def overridableInitializationStage(it : Initialize) : void {
		var physicSkill = new StandardPhysicEnvironment(it.parameters.get(0) as UUID,
			it.parameters.get(1) as UUID)
		this.behaviorType = it.parameters.get(2) as DynamicType
		setSkill(physicSkill, typeof(PhysicEnvironment))
	}
	
	def getMaxLinear(p : Percept) : float {
		if (this.behaviorType == DynamicType::STEERING) p.maxLinearAcceleration else p.maxLinearSpeed
	}
	
	def getMaxAngular(p : Percept) : float {
		if (this.behaviorType == DynamicType::STEERING) p.maxAngularAcceleration else p.maxAngularSpeed
	}

	def emitInfluence(output : MotionAlgorithmOutput, influences : Influence*) {
		if (output !== null) {
			if (output.getType() === DynamicType::STEERING)
				influenceSteering(output.linear, output.angular, influences)
			else
				influenceKinematic(output.linear, output.angular, influences);
		} else {
			influenceSteering(new Vector2f, 0f, influences)
		}
	}
	
	def doNothing {
		influenceKinematic(new Vector2f, 0f)
	}

	def first(list : List<Percept>) : Percept {
		if (list.isEmpty) null else list.get(0)
	}

	on StopSimulation {
		killMe
	}

}